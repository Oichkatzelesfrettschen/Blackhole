#version 460 core

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

struct DrawInstance {
  vec4 offsetScale;
  vec4 tint;
  vec4 flags;
};

layout(std430, binding = 3) readonly buffer DrawInstances {
  DrawInstance instances[];
};

struct DrawArraysIndirectCommand {
  uint count;
  uint primCount;
  uint first;
  uint baseInstance;
};

layout(std430, binding = 4) buffer DrawCommands {
  DrawArraysIndirectCommand commands[];
};

layout(std430, binding = 5) buffer DrawCount {
  uint drawCount;
};

uniform uint instanceCount = 0;

void main() {
  uint idx = gl_GlobalInvocationID.x;
  if (idx >= instanceCount) {
    return;
  }
  if (instances[idx].flags.x > 0.5) {
    uint writeIndex = atomicAdd(drawCount, 1);
    commands[writeIndex].count = 6;
    commands[writeIndex].primCount = 1;
    commands[writeIndex].first = 0;
    commands[writeIndex].baseInstance = idx;
  }
}
